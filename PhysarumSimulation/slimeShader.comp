#version 430 core

//layout (local_size_x = 30, local_size_y = 30, local_size_z = 1) in;
layout (local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

struct Agent
{
	float x;
	float y;
	float direction;
};

//function declarations
float sense(Agent currentAgent, float sensorAngle);
uint hash (uint x);
float uintToRange01(uint state);

//data passed from cpu
uniform float time;
uniform uint windowWidth;
uniform uint windowHeight;
layout (binding = 3, rgba32f) uniform image2D trailTexture;
layout (binding = 4, rgba32f) uniform image2D agentTexture;
layout (std430, binding = 1) buffer agentBuffer
{
    Agent agents[];
};

float moveSpeed = 1;
#define PI 3.1415926535897932384626433832795
float sensorDistance = 10;
float sensorAngle = PI / 4;
float turnSpeed = 0.628;

void main()
{
    ivec2 texelCoord = ivec2(gl_GlobalInvocationID.xy);

	uint index = gl_GlobalInvocationID.x;
	Agent currentAgent = agents[index];

	uint random = hash(int(currentAgent.y) * windowWidth + int(currentAgent.x) + hash(index + int(time) * 100000));
	float randomSteer = uintToRange01(hash(random));
	//sense
	float senseLeft = sense(currentAgent, sensorAngle);
	float senseForward = sense(currentAgent, 0);
	float senseRight = sense(currentAgent, -sensorAngle);

	//Sensed more left
	if (senseLeft > senseRight && senseLeft > senseForward)
	{
		currentAgent.direction += randomSteer * turnSpeed / 3;
	}
	//Sensed more right
	else if (senseRight > senseLeft && senseRight > senseForward)
	{
		currentAgent.direction -= randomSteer * turnSpeed / 3;
	}
	//Sensed more forward
	else if (senseForward > senseLeft && senseForward > senseRight)
	{
		currentAgent.direction += 0;
	}
	else if (senseLeft == 0 && senseRight == 0 && senseForward == 0)
	{
		currentAgent.direction += (randomSteer - 0.5) * turnSpeed / 3;
	}
	else 
	{
		currentAgent.direction += (randomSteer - 0.5) * turnSpeed / 3;
	}

	//calculate agent movement
	currentAgent.x += moveSpeed * cos(currentAgent.direction);
	currentAgent.y += moveSpeed * sin(currentAgent.direction);

	//wall detection
	if (currentAgent.x >= windowWidth || currentAgent.x <= 0)
	{
		currentAgent.x = min(windowWidth - 1, max(0, currentAgent.x));
		currentAgent.direction = uintToRange01(hash(random)) * PI;
	}
	if (currentAgent.y >= windowHeight || currentAgent.y <= 0)
	{
		currentAgent.y = min(windowHeight - 1, max(0, currentAgent.y));
		currentAgent.direction = uintToRange01(hash(random)) * PI;
	}
	imageStore(trailTexture, ivec2(currentAgent.x, currentAgent.y), vec4(1, 1, 1, 1));

	agents[index] = currentAgent;

	imageStore(agentTexture, ivec2(currentAgent.x, currentAgent.y), vec4(.1176, .3176, .6627, 1));
	imageStore(agentTexture, ivec2(currentAgent.x, currentAgent.y), vec4(1, 1, 1, 1));
}

float sense(Agent currentAgent, float sensorAngle)
{
	float sensorDirection = sensorAngle + currentAgent.direction;
	ivec2 sensorPosition = ivec2(currentAgent.x + cos(sensorDirection) * sensorDistance, currentAgent.y + sin(sensorDirection) * sensorDistance);
	vec4 values = imageLoad(trailTexture, sensorPosition);
	return values.x + values.y + values.z;
}

//Jenkins hash function
uint hash( uint x ) {
    x += ( x << 10u );
    x ^= ( x >>  6u );
    x += ( x <<  3u );
    x ^= ( x >> 11u );
    x += ( x << 15u );
    return x;
}

float uintToRange01(uint state)
{
	//scale unsigned int to 0-1 range
	float res = state / 4294967295.f;
	return res;
}